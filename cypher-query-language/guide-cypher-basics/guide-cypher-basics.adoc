= Guide to Cypher Basics
:level: Beginner
:toc:
:toc-placement!:
:toc-title: Overview
:toclevels: 1
:section: Cypher Query Language

.Goals
[abstract]
We live in a connected world. There are no isolated pieces of information, but rich, connected domains all around us.

.Prerequisites
[abstract]
Nodes can be tagged with zero to many labels representing their different roles in the domain.
In addition to contextualizing node and relationship properties, labels may also serve to attach metadata—​index or constraint information, for example—​to nodes.

[role=expertise]
{level}

toc::[]

=== Pop-Cultural Connections
_The Movie Graph_ is a mini graph application containing actors and directors that are related through the movies they've collaborated on.

This guide will show you how to:

1. Create: insert movie data into the graph
2. Find: retrieve individual movies and actors
3. Query: discover related actors and directors
4. Solve: the Bacon Path

[role=side-nav]
=== Recommended

[role=recommended]
* http://neo4j.com/docs[The Neo4j Docs]
* link:/blog[The Neo4j Blog]
* link:/build-a-graph-data-model/guide-intro-to-graph-modeling[Intro to Graph Modeling]

=== Create

Hidden below this text is a giant code block containing a single Cypher query statement composed of multiple `CREATE` clauses.
This will create the movie graph.

(You can unfold it with the little [role='icon-plus-sign-alt']plus button)

<<CYPHER,+:help Cypher+>>, <<CREATE,+:help CREATE+>>

=== Find

Example queries for finding individual nodes.

1. Look on every query example
2. Run the query with the play button
3. Notice the syntax pattern
4. Try looking for other movies or actors

<<MATCH,+:help MATCH+>>, <<WHERE,+:help WHERE+>>, <<RETURN,+:help RETURN+>>

==== Find the Actor Named "Tom Hanks"...

[source,cypher]
----
MATCH (tom {name: "Tom Hanks"})
RETURN tom
----

//graph_result
//table

==== Find the Movie Titled "Cloud Atlas"...

[source,cypher]
----
MATCH (cloudAtlas {title: "Cloud Atlas"})
RETURN cloudAtlas
----

//graph_result

//table

==== Find 10 People...

[source,cypher]
----
MATCH (people:Person)
RETURN people.name LIMIT 10
----

// table

==== Find Movies Released in the 1990s...

[source,cypher]
----
MATCH (nineties:Movie)
WHERE nineties.released > 1990 AND nineties.released < 2000
RETURN nineties.title
----

//table

=== Query

Finding patterns within the graph.

1. Actors are people who acted in movies
2. Directors are people who directed a movie
3. What other relationships exist

<<MATCH,+:help MATCH+>>

==== List All Tom Hanks Movies...

[source,cypher]
----
MATCH (tom:Person {name: "Tom Hanks"})-[:ACTED_IN]->(tomHanksMovies)
RETURN tom,tomHanksMovies
----

//graph_result

//table

==== Who Directed "Cloud Atlas"?

[source,cypher]
----
MATCH (cloudAtlas {title: "Cloud Atlas"})<-[:DIRECTED]-(directors)
RETURN directors.name
----

//table

==== Tom Hanks' Co-Actors...

[source,cypher]
----
MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors)
RETURN tom, m, coActors
----

//graph_result

//table

==== How people are related to "Cloud Atlas"...

[source,cypher]
----
MATCH (people:Person)-[relatedTo]-(:Movie {title: "Cloud Atlas"})
RETURN people.name, Type(relatedTo), relatedTo
----

//table

=== Solve

You've heard of the classic "Six Degrees of Kevin Bacon"? That is simply a shortest path query called the "Bacon Path".

1. Variable length patterns
2. Built-in shortestPath() algorithm

http://docs.neo4j.org/chunked/stable/query-match.html#match-variable-length-relationships[variable length relationships]
http://docs.neo4j.org/chunked/stable/query-match.html#match-shortest-path[shortestPath]

==== Movies and Actors up to Three Hops Away from Kevin Bacon

[source,cypher]
----
MATCH (bacon:Person {name:"Kevin Bacon"})-[*1..3]-(hollywood)
RETURN DISTINCT hollywood
----

//graph_result

//table

==== The Bacon Path, to Meg Ryan

[source,cypher]
----
MATCH p=shortestPath(
  (bacon:Person {name:"Kevin Bacon"})-[*]-(meg:Person {name:"Meg Ryan"})
)
RETURN p
----

//graph_result

//table

=== Clean Up

When you're done experimenting, you can remove the movie data set.

[NOTE]
1. Nodes can't be deleted if relationships exist
2. Delete both nodes and relationships together

[WARNING]
This will remove all Person and Movie nodes!

<<DELETE,+:help DELETE+>>

==== Delete all Movie and Person nodes and their Relationships

//output
[source,cypher]
----
MATCH (a:Person),(m:Movie)
OPTIONAL MATCH (a)-[r1]-(), (m)-[r2]-()
DELETE a,r1,m,r2
----

==== Prove that the Movie Graph is Gone

[source,cypher]
----
MATCH (n)
RETURN count(*)
----

//table

[role=side-nav]
=== Further Reading

* link:/books[The Neo4j Bookshelf]
* http://watch.neo4j.org[The Neo4j Video Library]
* http://gist.neo4j.org/[GraphGists]
