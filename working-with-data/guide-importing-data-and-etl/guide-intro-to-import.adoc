= Importing Data into Neo4j
:level: Beginner
:toc:
:toc-placement!:
:toc-title: Overview
:toclevels: 1
:section: Data Import

.Goals
[abstract]
Existing data comes in many shapes and sizes and one of the most popular is a relational database.
In this guide we'll look at how we can export a Postgres data set into Neo4j.

.Prerequisites
[abstract]
You should have a basic understanding of the link:/what-is-neo4j/property-graph[property graph model] and have completed the link:../../build-a-graph-data-model/guide-intro-to-graph-modeling/index.html[modeling guide].
Having link:/download[downloaded] and installed Neo4j server helps you code along with the examples.

[role=expertise]
{level}

=== Northwind Introduction

In this guide we'll be using the link:https://code.google.com/p/northwindextended/downloads/detail?name=northwind.postgre.sql&can=2&q=[NorthWind data set] as our example.
NorthWind is a popular data set used to demonstrate SQL and relational databases and is graphy enough to be interesting for us.

The following is an entity relationship diagram of the data set:

image:{img}/Northwind_diagram.jpg[]

=== Developing a Graph Model

There are a couple of rules of thumbs when deriving a graph model from a relational one which we can use to come up with our first cut:

* A row is a node
* A table name is a label name

In this data set, the following graph model serves as a first iteration:

image:{img}/northwind_graph_simple.png[]

==== How does the Graph Model Differ from the Relational Model?

* Graph should be more normalised. e.g. addresses have been denormalised in the Suppliers, Orders, Customers, and Employees tables.
* No nulls in the graph model - in the relational one to track the employee hierarchy we have a null entry in the 'ReportsTo' column if they don't report to anybody

=== Exporting the Data to CSV

Now that we know what we'd like our graph to look like we need to extract the data from PostgreSQL so we can create it.
The easiest way to do that is to export the appropriate tables in CSV format. Postgres' 'copy' command lets us execute a SQL query and write the result to a CSV file:

[source, sql]
----
COPY (SELECT * FROM customers) TO '/tmp/customers.csv' WITH CSV header;
COPY (SELECT * FROM suppliers) TO '/tmp/suppliers.csv' WITH CSV header;
COPY (SELECT * FROM products)  TO '/tmp/products.csv' WITH CSV header;
COPY (SELECT * FROM employees) TO '/tmp/employees.csv' WITH CSV header;
COPY (SELECT * FROM categories) TO '/tmp/categories.csv' WITH CSV header;

COPY (SELECT * FROM orders
      LEFT OUTER JOIN order_details ON order_details."OrderID" = orders."OrderID") TO '/tmp/orders.csv' WITH CSV header;
----

[role=side-nav]
=== Recommended

* http://neo4j.com/docs[The Neo4j Docs]
* link:/blog[The Neo4j Blog]
* link:/build-a-graph-data-model/guide-intro-to-graph-modeling[Intro to Graph Modeling]

=== Importing the Data using Cypher

After we've done exported our data from PostgreSQL, we'll use Cypher's link:http://docs.neo4j.org/chunked/stable/query-load-csv.html[LOAD CSV] command to transform the contents of the CSV file into a graph structure.

We'll start by creating the nodes:

[source, cypher]
----
# Create customers
LOAD CSV WITH HEADERS FROM "file:/tmp/customers.csv" AS row
CREATE (:Customer {CompanyName: row.CompanyName, CustomerID: row.CustomerID, Fax: row.Fax, Phone: row.Phone});

# Create products
LOAD CSV WITH HEADERS FROM "file:/tmp/products.csv" AS row
CREATE (:Product {ProductName: row.ProductName, ProductID: row.ProductID, UnitPrice: row.UnitPrice});

# Create suppliers
LOAD CSV WITH HEADERS FROM "file:/tmp/suppliers.csv" AS row
CREATE (:Supplier {CompanyName: row.CompanyName, SupplierID: row.SupplierID});

# Create employees
LOAD CSV WITH HEADERS FROM "file:/tmp/employees.csv" AS row
CREATE (:Employee {EmployeeID:row.EmployeeID,  FirstName: row.FirstName, LastName: row.LastName, Title: row.Title});

# Create categories
LOAD CSV WITH HEADERS FROM "file:/tmp/categories.csv" AS row
CREATE (:Category {CategoryID: row.CategoryID, CategoryName: row.CategoryName, Description: row.Description});
----

The next step is to create the relationships between these nodes. Before we can do this, however, we'll create indexes on the nodes that we've just created to ensure their quick lookup when creating relationships in the next section.

[source, cypher]
----
CREATE INDEX ON :Product(ProductID);
CREATE INDEX ON :Category(CategoryID);
CREATE INDEX ON :Employee(EmployeeID);
CREATE INDEX ON :Supplier(SupplierID);
CREATE INDEX ON :Customer(CustomerID);
----

With those indices in place we can now create orders and their relationships to products and employees:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:/tmp/orders.csv" AS row
CREATE (order:Order {OrderID: row.OrderID, ShipName: row.ShipName})

WITH *
MATCH (product:Product {ProductID: row.ProductID})
MATCH (employee:Employee {EmployeeID: row.EmployeeID})

MERGE (employee)-[:SOLD]->(order)
MERGE (order)-[:PRODUCT]->(product);
----

Next we'll create relationships between products, suppliers, and categories:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:/tmp/products.csv" AS row
MATCH (product:Product {ProductID: row.ProductID})
MATCH (supplier:Supplier {SupplierID: row.SupplierID})
MATCH (category:Category {CategoryID: row.CategoryID})

MERGE (supplier)-[:SUPPLIES]->(product)
MERGE (product)-[:PART_OF]->(category);
----

Finally we'll create the 'REPORTS_TO' relationship between employees which will create the reporting structure in the graph:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:/tmp/employees.csv" AS row
MATCH (employee:Employee {EmployeeID: row.EmployeeID})
MATCH (manager:Employee {EmployeeID: row.ReportsTo})
MERGE (employee)-[:REPORTS_TO]->(manager);
----

For completeness we'll also create an index on orders so that we can look them up quickly in our queries:

[source, cypher]
----
CREATE INDEX ON :Order(OrderId);
----

A sample of the graph looks like this:

image:{img}/northwind_graph_sample.png[]

We can now query the graph...

=== Querying the Graph

One question we would love to answer is:

==== Which Employee had the Highest Cross-Selling Count of Which Product with 'Chocolade'

[source,cypher]
----
MATCH (choc:Product {name:'Chocolade'})<-[:PRODUCT]-(:Order)<-[:SOLD]-(employee),
      (employee)-[:SOLD]->()-[:PRODUCT]->(other:Product)
RETURN employee.employeeId, other.name, count(*) as count
ORDER BY count DESC
LIMIT 5
----

And as we can see, employee number 1 was really busy.

[format="csv", options="header"]
|===
employee.employeeId,other.name,count
1,Flotemysost,24
1,Gorgonzola Telino,22
1,Pavlova,22
1,Camembert Pierrot,22
1,Ikura,20
|===

[role=side-nav]
=== Further Reading

* link:/books[The Neo4j Bookshelf]
* http://watch.neo4j.org[The Neo4j Video Library]
* http://gist.neo4j.org/[GraphGists]
